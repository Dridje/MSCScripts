{
	const GIB_INFLICTER $get(ent_me,id)
	const EXPLODE_RADIUS 200
}

//Throws a bouncing exploding gib. Takes owner, velocity, damage, and blood color.
{ game_spawn

	invincible 1
	race beloved

	gravity 0.75

	height 10
	width 10
	
	setprop ent_me movetype 10
	setprop ent_me friction 0.4 //Just reduces bounce factor, doesnt reduce velocity if gliding on ground
	setprop ent_me solid 0
}

{ game_dynamically_created //<owner> <velocity> <damage> <skill> <"green"|"red">

	setvard FX_OWNER PARAM1
	setvelocity ent_me PARAM2
	setvard FX_DAMAGE PARAM3
	setvard FX_SKILL PARAM4

	//Check the blood color and set appropriate model chunks and colors
	if ( PARAM5 equals 'green' ) //If green, set to green
	{
		setmodel "agibs.mdl"
		setmodelbody 0 $rand(0,3) //Random body
	}
	else //In any other case, set to red.
	{
		setmodel $get_random_token('gib_b_bone.mdl;gib_b_gib.mdl;gib_lung.mdl;gib_legbone.mdl')
	}
	
	callevent 3.5 explode
}

{ explode

	//<target|(src_origin)> <range|aoe|(dest_origin)|direct> <damage> <cth|fall_off> <attacker> <inflciter> <skill|none> <dmg_type> [flag_string]
	xdodamage $get(ent_me,origin) EXPLODE_RADIUS FX_DAMAGE 0 FX_OWNER GIB_INFLICTER FX_SKILL dark dmgevent:*gib
	clientevent new all effects/swords_gb/sfx_gib_explode $get(ent_me,origin)
	
	setmodel null.mdl //Takes a whole frame to get deleted, so set invisible
	callevent 0.1 remove_fx //delayed removal so gib_dodamage can do its thing
}

{ remove_fx
	deleteme
}

{ //reduces velocity slightly
	repeatdelay 0.3
	
	//if $vec.z($get(ent_me,velocity)) > 0
	local L_VEL $get(ent_me,velocity)
	vectormultiply L_VEL $vec(0.6,0.6,0.8)
	
	setvelocity ent_me L_VEL
}
