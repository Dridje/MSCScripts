//Handles voting system on the game master.
//-Votes can inlcude votemap change, votekick, voteban, vote lock...

#scope server

{ gm_generic_vote  //<caller_id> <token_options> <title> [descript] [silent:0|1]

	name VOTE!

	setvard VOTE_BUSY 1

	setvard VOTE_OVER 0
	setvard VOTE_STARTER PARAM1
	setvard VOTE_OPTIONS PARAM2
	setvard VOTE_TITLE PARAM3
	setvard VOTE_DESC PARAM4
	setvard VOTE_SILENT PARAM5
	setvard VOTE_WINNAR_IDX 0
	setvard VOTE_WINNAR_COUNT 0
	if ( VOTE_DESC equals 'PARAM4' ) setvard VOTE_DESC " "

	setvard VOTE_TALLY ''
	calleventloop $get_token_amt(VOTE_OPTIONS) gm_clear_ballots
	if ( $get_token_amt(VOTE_TALLY) >  $get_token_amt(VOTE_OPTIONS) ) token.del VOTE_TALLY 0

	infomsg all VOTE_TITLE VOTE_DESC

	setvard MENU_MODE vote_generic

	getplayers GM_PLAYER_LIST
	calleventloop $get_token_amt(GM_PLAYER_LIST) gm_send_ballots
	setvard VOTE_IN_PROGRESS 1
	callevent 15.0 gm_tally_votes
}

{ gm_ynvote //<caller_id> <ratio_req> [gm_event_yes] [gm_event_no] - sets gmvar VOTE_PASSED -1|0|1 (notevotes)|voted_down|passed
	//simplified yes no vote calls a gm master side event if yn ratio > PARAM2

	name VOTE!

	setvard VOTE_QUIET_MODE 1

	setvard VOTE_BUSY 1

	setvard VOTE_OVER 0
	setvard VOTE_STARTER PARAM1
	setvard VOTE_REQ PARAM2
	setvard EXEC_YES PARAM3
	setvard EXEC_NO PARAM4

	dbg gm: $get(VOTE_STARTER,name) started_mapvote: VOTE_TITLE ( MAP_NAME ) fast QUICK_CHANGE

	setvard YES_VOTES 0
	setvard NO_VOTES 0
	setvard TOTAL_VOTES 0
	setvard TOTAL_VOTES 0

	setvard MENU_MODE yn

	getplayers GM_PLAYER_LIST
	//if ( G_DEVELOPER_MODE ) messageall g sending message to GM_PLAYER_LIST MENU_MODE
	calleventloop $get_token_amt(GM_PLAYER_LIST) gm_send_ballots
	setvard VOTE_IN_PROGRESS 1
	callevent 15.0 gm_tally_votes
}

{ game_menu_getoptions

	if ( MENU_MODE equals votemap )
	{
		local reg.mitem.title 	"Yes!"
		local reg.mitem.type 	callback
		local reg.mitem.callback gm_got_yes_vote
		menuitem.register

		local reg.mitem.title 	"No!"
		local reg.mitem.type 	callback
		local reg.mitem.callback gm_got_no_vote
		menuitem.register
	}

	if ( MENU_MODE equals yn )
	{
		local reg.mitem.title 	"Yes!"
		local reg.mitem.type 	callback
		local reg.mitem.callback gm_got_yes_vote
		menuitem.register

		local reg.mitem.title 	"No!"
		local reg.mitem.type 	callback
		local reg.mitem.callback gm_got_no_vote
		menuitem.register
	}

	if ( MENU_MODE equals vote_generic )
	{
		calleventloop $get_token_amt(VOTE_OPTIONS) gm_build_ballot
	}

}

{ gm_build_ballot

	local OPTION_N game.script.iteration

	local CUR_OPTION $get_token(VOTE_OPTIONS,OPTION_N)

	local reg.mitem.title 	CUR_OPTION
	local reg.mitem.type 	callback
	local reg.mitem.data	OPTION_N
	local reg.mitem.callback gm_gvote_count
	menuitem.register
}

{ gm_clear_ballots
	stradd VOTE_TALLY "0;"
}

{ gm_votekick //<target> <caller> - called by sv_admin

	setvard VOTE_BUSY 1

	setvard GM_SKIP_VOTE_ID PARAM1
	local VOTE_STARTER PARAM2
	local L_VOTE_TITLE "KICK: "
	stradd L_VOTE_TITLE $get(GM_SKIP_VOTE_ID,name)
	local L_VOTE_DESC $get(VOTE_STARTER,name)
	strconc L_VOTE_DESC " " has started a kick vote against $get(GM_SKIP_VOTE_ID,name)
	infomsg all L_VOTE_TITLE L_VOTE_DESC
	callevent gm_ynvote $get(VOTE_STARTER,id) 1.01
	//callevent gm_generic_vote $get(VOTE_STARTER,id) "Yes;No" L_VOTE_TITLE L_VOTE_DESC 1
}

{ gm_voteban //<target> <caller> - called by sv_admin

	setvard VOTE_BUSY 1

	setvard GM_SKIP_VOTE_ID PARAM1
	local VOTE_STARTER PARAM2
	local L_VOTE_TITLE "BAN: "
	stradd L_VOTE_TITLE $get(GM_SKIP_VOTE_ID,name)
	local L_VOTE_DESC $get(VOTE_STARTER,name)
	strconc L_VOTE_DESC " " has started a ban vote against $get(GM_SKIP_VOTE_ID,name)
	callevent gm_ynvote $get(VOTE_STARTER,id) 1.01
	//callevent gm_generic_vote $get(VOTE_STARTER,id) "Yes;No" L_VOTE_TITLE L_VOTE_DESC 1
}

{ [server] gm_map_vote //<caller_id> [map] [title] <change_map_from_here:1|0> <quiet:0|1>

	setvard VOTE_FAILED 0
	setvard VOTE_BUSY 1
	setvard VOTE_TYPE map
	setvard VOTE_OVER 0
	setvard VOTE_STARTER PARAM1
	setvard MAP_NAME PARAM2
	setvard VOTE_TITLE PARAM3
	setvard QUICK_CHANGE PARAM4 //gm will change map
	setvard VOTE_QUIET_MODE PARAM5 //don't spam infomsg - use hud

	name VOTE_TITLE

	dbg gm: $get(VOTE_STARTER,name) started_mapvote: VOTE_TITLE ( MAP_NAME ) fast QUICK_CHANGE

	setvard YES_VOTES 0
	setvard NO_VOTES 0
	setvard TOTAL_VOTES 0
	setvard TOTAL_VOTES 0

	setvard MENU_MODE votemap

	getplayers GM_PLAYER_LIST
	//if ( G_DEVELOPER_MODE ) messageall g sending message to GM_PLAYER_LIST MENU_MODE
	calleventloop $get_token_amt(GM_PLAYER_LIST) gm_send_ballots
	setvard VOTE_IN_PROGRESS 1
	callevent 15.0 gm_tally_votes
}

{ gm_send_ballots

	local CUR_PLAYER $get_token(GM_PLAYER_LIST,game.script.iteration)
	//if GM_SKIP_VOTE_ID isnot CUR_PLAYER //for kic/ban votes - target does not get to vote
	menu.open CUR_PLAYER
}


{ gm_gvote_count

	if ( VOTE_OVER ) dplayermessage PARAM1 Sorry, voting has already ended.

	if !VOTE_OVER

	local VOTE_INDEX PARAM2
	local CURIDX_COUNT $get_token(VOTE_TALLY,VOTE_INDEX)
	add CURIDX_COUNT 1
	token.set VOTE_TALLY VOTE_INDEX CURIDX_COUNT

	if ( !VOTE_QUIET_MODE )
	{
		local MSG_STRING $get(PARAM1,name)
		stradd MSG_STRING " says "
		stradd MSG_STRING $get_token(VOTE_OPTIONS,VOTE_INDEX)
		infomsg all MSG_STRING " "
	}

	if ( VOTE_QUIET_MODE )
	{
		local MSG_STRING $get(PARAM1,name)
		stradd MSG_STRING " votes for "
		stradd MSG_STRING $get_token(VOTE_OPTIONS,VOTE_INDEX)
		messageall green MSG_STRING
	}
}

{ gm_got_yes_vote

	if ( VOTE_OVER ) dplayermessage PARAM1 Sorry, voting has already ended.

	if !VOTE_OVER

	add TOTAL_VOTES 1
	add YES_VOTES 1

	local MSG_STRING $get(PARAM1,name)
	stradd MSG_STRING " votes yes."
	if ( !VOTE_QUIET_MODE ) infomsg all MSG_STRING " "
	if ( VOTE_QUIET_MODE ) messageall green MSG_STRING
}

{ gm_got_no_vote

	if ( VOTE_OVER ) dplayermessage PARAM1 Sorry, voting has already ended.

	if !VOTE_OVER

	add TOTAL_VOTES 1
	add NO_VOTES 1
	local MSG_STRING $get(PARAM1,name)
	stradd MSG_STRING " votes no."
	if ( !VOTE_QUIET_MODE ) infomsg all MSG_STRING " "
	if ( VOTE_QUIET_MODE ) messageall green MSG_STRING
}

{ gm_tally_votes

	setvard GM_SKIP_VOTE_ID 0
	if ( VOTE_TYPE isnot 'map' ) setvard VOTE_IN_PROGRESS 0

	name The Game Master
	setvard VOTE_OVER 1
	setvard VOTE_BUSY 0
	setvard VOTE_IN_PROGRESS 0

	if ( MENU_MODE equals votemap )
	{
		if ( TOTAL_VOTES == 0 )
		{
			infomsg all "Vote Failed" "No one voted."
			setvard VOTE_IN_PROGRESS 0
			setvard VOTE_FAILED 1
		}

		multiply NO_VOTES 2
		if ( NO_VOTES > YES_VOTES )
		{
			infomsg all "Vote Failed" "Too many nay sayers."
			setvard VOTE_IN_PROGRESS 0
			setvard VOTE_FAILED 1
		}

		if ( VOTE_FAILED ) callexternal VOTE_STARTER vote_failed

		if !VOTE_FAILED
		callexternal VOTE_STARTER vote_passed

		if QUICK_CHANGE

		local MSG_TITLE "TRAVELING TO "
		stradd MSG_TITLE MAP_NAME
		infomsg all MSG_TITLE "You will be reconnected shortly."
		local AMX_STRING force_map_
		stradd AMX_STRING MAP_NAME
		usetrigger AMX_STRING
		setvard GM_DISABLE_TRANSITIONS 1 //tells msarea_transitions to disable code side, so they don't change spawns
		if ( GM_DEST_TRANS isnot 'GM_DEST_TRANS' ) callexternal players ext_setspawn GM_DEST_TRANS
		//saveallnow was removed
		callevent 5.0 gm_manual_map_change
	}

	if ( MENU_MODE equals yn )
	{

		if ( G_DEVELOPER_MODE ) messageall green gm_ynvote: votes TOTAL_VOTES yes YES_VOTES no NO_VOTES
		if ( TOTAL_VOTES == 0 )
		{
			infomsg all "Vote Failed" "No one voted."
			setvard VOTE_FAILED 1
			setvard VOTE_PASSED -1
		}

		setvard VOTE_RATIO YES_VOTES
		if ( NO_VOTES > 0 ) divide VOTE_RATIO NO_VOTES
		if ( VOTE_RATIO >= VOTE_REQ )
		{
			setvard VOTE_PASSED 1
			if ( EXEC_YES startswith 'PARAM' ) local NO_EXEC 1
			if !NO_EXEC
			callevent EXEC_YES
		}
		if ( VOTE_RATIO < VOTE_REQ )
		{
			setvard VOTE_PASSED 0
			if ( EXEC_YES startswith 'PARAM' ) local NO_EXEC 1
			if !NO_EXEC
			callevent EXEC_NO
		}
	}

	if ( MENU_MODE equals vote_generic )
	{
		setvard CUR_HIGH 0
		calleventloop $get_token_amt(VOTE_TALLY) gm_find_highest
		if ( VOTE_WINNAR_COUNT == 0 ) setvard VOTE_FAILED 1

		if ( !VOTE_FAILED )
		{
			setvard VOTE_RESULT $get_token(VOTE_OPTIONS,VOTE_WINNAR_IDX)
			callexternal VOTE_STARTER vote_passed $get_token(VOTE_OPTIONS,VOTE_WINNAR_IDX)
		}
		if ( VOTE_FAILED ) 
		{
			setvard VOTE_RESULT "[failed]"
			callexternal VOTE_STARTER vote_failed "no_votes"
		}

		if !VOTE_SILENT
		if ( !VOTE_FAILED )
		{
			infomsg all $get_token(VOTE_OPTIONS,VOTE_WINNAR_IDX) "Has won the vote!"
		}
		else
		{
			infomsg all "VOTE FAILED" "No one voted."
		}
	}
}

{ vote_pvp_yes

	setvard VOTE_BUSY 0
	setvard VOTE_IN_PROGRESS 0

	if ( GM_VOTE_MODE equals 'pvp_on' )
	{
		infomsg all "PVP VOTE PASSED" "PvP mode will begin in 10 seconds..."
		callevent gm_start_pvp
	}

	if ( GM_VOTE_MODE equals 'pvp_off' )
	{
		infomsg all "PVP VOTE PASSED" "PvP mode will end in 60 seconds..."
		callevent gm_end_pvp
	}
}

{ vote_pvp_no
	setvard VOTE_BUSY 0
	setvard VOTE_IN_PROGRESS 0
	if ( VOTE_PASSED == 0 ) infomsg all "PVP VOTE FAILED" "Too many nay sayers."
	if ( VOTE_PASSED == -1 ) infomsg all "PVP VOTE FAILED" "No votes gathered."
}

{ gm_start_pvp
	setvard GM_COUNT_DOWN_TO 10
	setvard GM_COUNT_MESSAGE "seconds before PvP begins!"
	setvard GM_COUNT_DOWN_EVENT gm_pvp_on
	callevent 1.0 gm_count_down
}

{ gm_end_pvp
	setvard GM_COUNT_DOWN_TO 60
	setvard GM_COUNT_MESSAGE "seconds before PvP ends..."
	setvard GM_COUNT_DOWN_EVENT gm_pvp_off
	callevent 1.0 gm_count_down
}

{ gm_pvp_on
	infomsg all "PvP MODE IS ACIVE!" "Players may now damage one another."
	setpvp 1
	setvard GM_COUNT_DOWN_EVENT ''
}

{ gm_pvp_off
	infomsg all "PvP MODE DEACTIVATED" "Players may no longer damage one another."
	setpvp 0
	setvard GM_COUNT_DOWN_EVENT ''
}

{ gm_votepvp //<caller> <0|1>

	if ( PARAM2 == 1 )
	{
		local VOTE_STARTER PARAM1
		local L_VOTE_TITLE "ACTIVATE PVP MODE"
		local L_VOTE_DESC $get(VOTE_STARTER,name)
		strconc L_VOTE_DESC " " has started a vote to enable player vs player combat
		setvard GM_VOTE_MODE "pvp_on"
		infomsg all L_VOTE_TITLE L_VOTE_DESC
		callevent gm_ynvote $get(VOTE_STARTER,id) 1.01 vote_pvp_yes vote_pvp_no
	}

	if ( PARAM2 == 0 )
	{
		local VOTE_STARTER PARAM1
		local L_VOTE_TITLE "DEACTIVATE PVP MODE"
		local L_VOTE_DESC $get(VOTE_STARTER,name)
		strconc L_VOTE_DESC " " has started a vote to end player vs player combat
		setvard GM_VOTE_MODE "pvp_off"
		infomsg all L_VOTE_TITLE L_VOTE_DESC
		callevent gm_ynvote $get(VOTE_STARTER,id) 1.01 vote_pvp_yes vote_pvp_no
	}

	setvard VOTE_BUSY 1
}

{ gm_free_vote_system
	//external to free up vote system
	setvard VOTE_BUSY 0
	setvard VOTE_IN_PROGRESS 0
}

{ gm_lock_server_yes
	dbg lock_server

	//generate random password (4 nums, not real secure, but need something easy to copy-pasta or remember)
	setvard SV_LOCK_PASSWORD $rand(0,9)
	stradd SV_LOCK_PASSWORD $rand(0,9)
	stradd SV_LOCK_PASSWORD $rand(0,9)
	stradd SV_LOCK_PASSWORD $rand(0,9)

	setvarg G_SERVER_LOCKER CALLING_LOCKER
	setvarg G_SERVER_LOCKED 1

	servercmd sv_password SV_LOCK_PASSWORD

	local CL_CMD_STR "password "
	stradd CL_CMD_STR SV_LOCK_PASSWORD
	clientcmd all CL_CMD_STR

	local MSG_DESC "Password is: "
	stradd MSG_DESC SV_LOCK_PASSWORD
	consolemsg all MSG_DESC
	stradd MSG_DESC "|This has been sent to your console (copy it)."
	stradd MSG_DESC "|Server will remain locked until "
	stradd MSG_DESC $get(G_SERVER_LOCKER,name)
	stradd MSG_DESC " disconnects"
	stradd MSG_DESC "|or map changes."

	helptip all generic "SERVER HAS BEEN LOCKED" MSG_DESC
}

{ gm_lock_server_no
	if ( VOTE_RESULT equals '[failed]' )
	{
		local MSG_TITLE "Vote Lock has failed."
		local MSG_DESC "No one voted."
		infomsg all MSG_TITLE MSG_DESC
	}
	if ( VOTE_RESULT equals 'No' )
	{
		local MSG_TITLE "Vote Lock has failed."
		local MSG_DESC "Voted down."
		infomsg all MSG_TITLE MSG_DESC	
		local EXIT_SUB 1
	}
}