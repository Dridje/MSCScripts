//Healing Wave as a spell. greatguys1@OCT2022
{
	const NO_REGISTER 1

	setvar SPELL_SKILL_REQUIRED	10
	const SPELL_PREPARE_TIME	1
	const SPELL_ENERGYDRAIN	50
	const MANA_COST	20 //special internal handling

	const MELEE_ATK_DURATION 3.2

	const SPELL_SCRIPT		"effects/sfx_wave"
	precachefile 			SPELL_SCRIPT
}

#include items/magic_hand_base

{  spell_spawn

	name 		"Healing Wave"
	desc 		"A holy wave of Felewyn to bless your allies."
	
	setvard LAST_ATTACK game.time
}

{ [server] game_attack1

	local TIME_DIFF game.time
	subtract TIME_DIFF LAST_ATTACK
	if TIME_DIFF > MELEE_ATK_DURATION

	setvard LAST_ATTACK game.time

	splayviewanim ent_me ANIM_CAST
	playowneranim critical PLAYERANIM_PREPARE squatwalk1

	if ( $get(ent_owner,mp) < MANA_COST ) dplayermessage ent_owner Insufficient mana.
	if $get(ent_owner,mp) >= MANA_COST

	callevent make_wave
}

{ make_wave

	givemp $neg(MANA_COST)
	callexternal ent_owner mana_drain //update mana on player hud readout, if using hud feedback

	local OWNER_LOC $get(ent_owner,origin)
	vectorset OWNER_LOC z $get_ground_height(OWNER_LOC)
	
	local L_OWNER_YAW $get(ent_owner,viewangles)
	local L_OWNER_YAW $vec.yaw(L_OWNER_YAW)
	
	createnpc SPELL_SCRIPT OWNER_LOC $get(ent_owner,id) L_OWNER_YAW $get(ent_owner,skill.spellcasting.divination) spellcasting.divination //<owner> <yaw> <level cast as> <skill to level>
}
